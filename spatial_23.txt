model
{

#Likelihood
for (i in 1:n) {
	y[i] ~ dnorm(mu[i],tau)
	mu[i]<-theta[i]*vas_ab[i]
	log(theta[i])<-alpha + beta_sexo[sexo[i]] + beta_edad[edad[i]] + beta_cancer[cancer[i]] + beta_imc[imc[i]] + w[i]
	w0[i]<-0
	}

#Priors 
w[1:n] ~ spatial.exp(w0[], s1[], s2[], tau.w, phi,1)
tau.w ~ dgamma(0.1,0.1)
phi ~ dgamma(0.1,0.1)
alpha ~ dnorm(0,0.1)
tau ~ dgamma(0.1,0.1)
for (j in 1:2) { 
	beta_sexo[j] ~ dnorm(0,2) 
	beta_edad[j] ~ dnorm(0,2) 
	beta_cancer[j] ~ dnorm(0,2)
  }
for (j in 1:3) { 
  beta_imc[j] ~ dnorm(0,2)
  }

#for (i in 1:n) { 
#  for (j in 1:n) {
#	  H[i,j]<-(1/tau.w)*exp(-phi*d[i,j])
#  }
#}
#Omega[1:n,1:n]<-inverse(H[1:n,1:n])

#Prediction 1
for (i in 1:n) { yf1[i] ~ dnorm(mu[i],tau) }

#Prediction 2
for (i in 1:m) { 
	yf2[i] ~ dnorm(muf[i],tau) 
	muf[i]<-theta_f[i]*vas_abf[i]
	log(theta_f[i])<-alpha+ beta_sexo[sexof[i]] + beta_edad[edadf[i]] + beta_cancer[cancerf[i]] + beta_imc[imcf[i]]+wf[i]
	wf[i] ~ spatial.unipred(w0f[i],s1f[i],s2f[i],w[])
	w0f[i]<-0
}

}
